/////////////////////////////////////////////////////
// normal map shader with phong specular highlighting
/////////////////////////////////////////////////////

//GLES 1.0 / WebGL

#version 100
precision mediump float;
varying vec2 vTexCoord;
varying vec3 vLightVec;
varying vec3 vEyeVec;
varying vec4 vShadowPosition;

uniform sampler2D uColourMap;
uniform sampler2D uSpecularMap;
uniform sampler2D uNormalMap;

uniform vec3 uSpecularColour;
uniform vec3 uDiffuseColour;
uniform vec3 uAmbientColour;

#include inc_ShadowMap.txt

void main(void)
{
	vec4 baseColour = texture2D(uColourMap, vTexCoord.xy);
	vec3 normal = normalize(texture2D(uNormalMap, vTexCoord.xy).xyz * 2.0 - 1.0);

	vec3 lightDir = normalize(vLightVec);
	vec3 eyeDir = normalize(vEyeVec);
	vec3 reflectDir = reflect(-lightDir, normal);

	float specAmount = 0.0;
	float diffuseAmount = max(dot(normal, lightDir), 0.0);
	if(diffuseAmount > 0.0)
	{
		vec4 specSample = texture2D(uSpecularMap, vTexCoord.xy);
		specAmount = pow(max(dot(reflectDir, lightDir), 0.0), specSample.g * 255.0) * specSample.r;  //was eyeDir not light dir
	}

	highp float bias = 0.000000001 * tan(acos(diffuseAmount));
	float visibility = getVisibility(vShadowPosition, bias);
	diffuseAmount *= visibility;
	specAmount = (visibility < 1.0) ? 0.0 : specAmount;

	vec3 finalColour = uAmbientColour + (baseColour.rgb * diffuseAmount) + (uSpecularColour * specAmount);
	gl_FragColor =  vec4(finalColour, baseColour.a);
}